/////////////////////////////////////
/////////////WEAPON//////////////////
/////////////////////////////////////

Actor Bow : Weapon
{
 game Doom
 //$Category Weapons
//$Title Bow
Inventory.PickUpMessage "Bow"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Weapon.AmmoType "ArrowAmmo"
+WEAPON.NOALERT
States
{
Select:
   TNT1 A 0 A_JumpIfInventory("IceSpellInv",1,"Select_Ice")
   TNT1 A 0 A_JumpIfInventory("FireSpellInv",1,"Select_Fire")
   TNT1 A 0 A_JumpIfInventory("ThunderSpellInv",1,"Select_Thunder")
   DBOW A 1 A_Raise
   Loop
Select_Ice:
   PBOW A 1 A_Raise
   Loop
Select_Fire:
   PBO2 A 1 A_Raise
   Loop
Select_Thunder:
   PBO3 A 1 A_Raise
   Loop
Deselect:
   TNT1 A 0 A_JumpIfInventory("IceSpellInv",1,"Deselect_Ice")
   TNT1 A 0 A_JumpIfInventory("FireSpellInv",1,"Deselect_Fire")
   TNT1 A 0 A_JumpIfInventory("ThunderSpellInv",1,"Deselect_Thunder")
   DBOW A 1 A_Lower
   Loop
Deselect_Ice:
   PBOW A 1 A_Lower
   Loop
Deselect_Fire:
   PBO2 A 1 A_Lower
   Loop
Deselect_Thunder:
   PBO3 A 1 A_Lower
   Loop
Ready:
   TNT1 A 0 A_JumpIfInventory("IceSpellInv",1,"Ready_Ice")
   TNT1 A 0 A_JumpIfInventory("FireSpellInv",1,"Ready_Fire")
   TNT1 A 0 A_JumpIfInventory("ThunderSpellInv",1,"Ready_Thunder")
   DBOW A 1 A_WeaponReady
   Loop
Ready_Ice:
   PBOW AAAEEE 1 A_WeaponReady
   Goto Ready
Ready_Fire:
   PBO2 AAAEEE 1 A_WeaponReady
   Goto Ready
Ready_Thunder:
   PBO3 AAAEEE 1 A_WeaponReady
   Goto Ready
Spawn:
	BWPU A -1
	Stop
Fire:
	TNT1 A 0 A_JumpIfInventory("IceSpellInv",1,"Fire_Ice")
	TNT1 A 0 A_JumpIfInventory("FireSpellInv",1,"Fire_Fire")
	TNT1 A 0 A_JumpIfInventory("ThunderSpellInv",1,"Fire_Thunder")
	DBOW B 3
	DBOW C 3 A_PlayWeaponSound("BOWREADY")
	Goto Hold
Hold:
	TNT1 A 0 A_JumpIfInventory("IceSpellInv",1,"Hold_Ice")
	TNT1 A 0 A_JumpIfInventory("FireSpellInv",1,"Hold_Fire")
	TNT1 A 0 A_JumpIfInventory("ThunderSpellInv",1,"Hold_Thunder")
	DBOW D 5
	TNT1 A 0 A_Refire
	TNT1 A 0 A_FireCustomMissile("ArrowProj",0,1,5,0)
	DBOW E 3 A_PlayWeaponSound("ARROWNRM")
	DBOW FBA 3
	Goto Ready
Fire_Ice:
	PBOW B 3
	PBOW C 3 A_PlayWeaponSound("BOWREADY")
	Goto Hold
Hold_Ice:
	PBOW DF 3
	TNT1 A 0 A_Refire
	TNT1 A 0 A_FireCustomMissile("IceArrow",0,1,5,0)
	DBOW E 3 A_PlayWeaponSound("ARROWICE")
	DBOW F 3
	PBOW BA 3
	Goto Ready
Fire_Fire:
	PBO2 B 3
	PBO2 C 3 A_PlayWeaponSound("BOWREADY")
	Goto Hold
Hold_Fire:
	PBO2 DF 3
	TNT1 A 0 A_Refire
	TNT1 A 0 A_FireCustomMissile("FireArrow",0,1,5,0)
	DBOW E 3 A_PlayWeaponSound("ARROWFIR")
	DBOW F 3
	PBO2 BA 3
	Goto Ready
Fire_Thunder:
	PBO3 B 3
	PBO3 C 3 A_PlayWeaponSound("BOWREADY")
	Goto Hold
Hold_Thunder:
	PBO3 DF 3
	TNT1 A 0 A_Refire
	TNT1 A 0 A_FireCustomMissile("ShockArrow",0,1,5,0)
	DBOW E 3 A_PlayWeaponSound("ARROWELE")
	DBOW F 3
	PBO3 BA 3
	Goto Ready
}
}

/////////////////////////////////////
/////////////PROJECTILES/////////////
/////////////////////////////////////

Actor ArrowProj
{
Radius 2
Height 2
Damage 5
Speed 36
PROJECTILE
+THRUGHOST
+DONTREFLECT
+RIPPER
Obituary "%o was perforated by one of %k's arrows"
Scale 0.8
States
{
Spawn:
	ARR0 A 1
	Loop
Death:
	TNT1 A 1
	Stop
}
}
Actor IceArrow : ArrowProj
{
Damage 20
Radius 5
Height 5
-THRUGHOST
-DONTREFLECT
-RIPPER
+ICEDAMAGE
DeathSound "IROWIMPC"
Obituary "%o was frozen by one of %k's ice arrows"
States
{
Spawn:
	ARR2 A 2 bright A_SpawnItem("IceArrowTrail",0,0,0)
	Loop
Death:
	TNT1 A 0 A_AlertMonsters
	SHEX ABCDE 3 bright
	Stop
}
}
Actor IceArrowTrail
{
Renderstyle Add
+NOGRAVITY
States
{
Spawn:
	ICPR IJKLM 3
	Stop
}
}
Actor FireArrow : IceArrow
{
-ICEDAMAGE
+FIREDAMAGE
+ROCKETTRAIL
Damage 28
Obituary "%o was burned by one of %k's fire arrows"
DeathSound "FROWIMPC"
Speed 30
States
{
Spawn:
	TNT1 A 0 A_AlertMonsters
	ARR3 A 2 bright
	Loop
Death:
	TNT1 A 0 A_SetTranslucent(0.6,1)
	FE01 A 3 bright A_Explode(30,50)
	FE01 BCDEFGHIJ 3 bright
	Stop
}
}
Actor ShockArrow : IceArrow
{
-ICEDAMAGE
+DONTREFLECT
+RIPPER
Damage 15
Obituary "%o was shocked by one of %k's thunder arrows"
DeathSound "EROWIMPC"
Speed 50
States
{
Spawn:
	ARR4 A 1 bright A_SpawnItem("ElectroTrail",0,0,0)
	Loop
Death:
	TNT1 A 1 A_AlertMonsters
	ZP01 ABCDEFG 4 Bright
	Stop
}
}

Actor ElectroTrail : IceArrowTrail
{
Scale 0.2
States
{
Spawn:
	ZP01 DEFG 2 Bright 
	Stop
}
}
/////////////////////////////////////
/////////////AMMO////////////////////
/////////////////////////////////////

Actor ArrowAmmo : ammo
{
Inventory.PickupMessage "Arrows"
Inventory.Amount 3
Inventory.MaxAmount 50
Ammo.BackpackAmount 6
Ammo.BackpackMaxAmount 100
Inventory.Icon "ARRWIMG"
States
{
Spawn:
	ARAM A -1
	Stop
}
}
Actor ArrowHefty : ArrowAmmo
{
Inventory.PickupMessage "Quiver of arrows"
Inventory.Amount 10
States
{
Spawn:
	ARA2 A -1
	Stop
}
}

/////////////////////////////////////
/////////////ARTIFACTS///////////////
/////////////////////////////////////

ACTOR IceSpell : CustomInventory
{
Inventory.Icon "SPELICN1"
Inventory.PickupMessage "Ice Spell"
Inventory.PickupSound "misc/p_pkup"
Inventory.Amount 1
Inventory.MaxAmount 1
+NOGRAVITY
+FLOATBOB
+INVENTORY.PICKUPFLASH
+INVENTORY.INVBAR
+INVENTORY.FANCYPICKUPSOUND
Inventory.DefMaxAmount
States
{
Spawn:
   ICE1 ABCB 6 Bright
    Loop
Use:
	TNT1 A 0 A_TakeInventory("FireSpellInv",1)
	TNT1 A 0 A_TakeInventory("ThunderSpellInv",1)
	TNT1 A 1 A_GiveInventory("IceSpellInv",1)
	Stop
}
}
ACTOR IceSpellInv : Inventory
{
-INVENTORY.INVBAR
States
{
Spawn:
    NULL A 0
	Stop
}
}
ACTOR FireSpell : CustomInventory
{
Inventory.Icon "SPELICN3"
Inventory.PickupMessage "Fire Spell"
Inventory.PickupSound "misc/p_pkup"
Inventory.Amount 1
Inventory.MaxAmount 1
+NOGRAVITY
+FLOATBOB
+INVENTORY.PICKUPFLASH
+INVENTORY.INVBAR
+INVENTORY.FANCYPICKUPSOUND
Inventory.DefMaxAmount
States
{
Spawn:
   FRE1 ABCB 6 Bright
    Loop
Use:
	TNT1 A 0 A_TakeInventory("IceSpellInv",1)
	TNT1 A 0 A_TakeInventory("ThunderSpellInv",1)
	TNT1 A 1 A_GiveInventory("FireSpellInv",1)
	Stop
}
}
ACTOR FireSpellInv : Inventory
{
-INVENTORY.INVBAR
States
{
Spawn:
    NULL A 0
	Stop
}
}
ACTOR ThunderSpell : CustomInventory
{
Inventory.Icon "SPELICN2"
Inventory.PickupMessage "Thunder Spell"
Inventory.PickupSound "misc/p_pkup"
Inventory.Amount 1
Inventory.MaxAmount 1
+NOGRAVITY
+FLOATBOB
+INVENTORY.PICKUPFLASH
+INVENTORY.INVBAR
+INVENTORY.FANCYPICKUPSOUND
Inventory.DefMaxAmount
States
{
Spawn:
   ELK1 ABCDEF 5 Bright
   ELK1 G 14 Bright
    Loop
Use:
	TNT1 A 0 A_TakeInventory("IceSpellInv",1)
	TNT1 A 0 A_TakeInventory("FireSpellInv",1)
	TNT1 A 1 A_GiveInventory("ThunderSpellInv",1)
	Stop
}
}
ACTOR ThunderSpellInv : Inventory
{
-INVENTORY.INVBAR
States
{
Spawn:
    NULL A 0
	Stop
}
}
Actor SpellBreaker : CustomInventory
{
Inventory.Icon "SPELICN4"
Inventory.PickupMessage "Spell Breaker"
Inventory.PickupSound "misc/p_pkup"
Inventory.Amount 1
Inventory.MaxAmount 1
+NOGRAVITY
+FLOATBOB
+INVENTORY.PICKUPFLASH
+INVENTORY.INVBAR
+INVENTORY.FANCYPICKUPSOUND
Inventory.DefMaxAmount
States
{
Spawn:
   UNSP A 1
    Loop
Use:
	TNT1 A 0 A_TakeInventory("IceSpellInv",1)
	TNT1 A 0 A_TakeInventory("FireSpellInv",1)
	TNT1 A 0 A_TakeInventory("ThunderSpellInv",1)
	TNT1 A 1 A_PlaySound("UNSPELL")
	Stop
}
}


//axe

ACTOR Axe : Weapon
{
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	Inventory.Icon "AXEPA0"
	Inventory.PickupMessage "You claimed yourself a powerful axe!"
	Tag "Axe"
	Obituary "%k butchered %o with his axe!"
	States
	{
	Spawn:
		AXEP A -1
		Stop
	Select:
		AXEG AAAAAAAAAA 0 A_Raise //To get it to raise
		TNT1 A 6
		AXEG EDCBA 2
		Goto Ready
	Deselect:
		TNT1 A 0 A_TakeInventory("AxeCharge", 0x7FFFFFFF)
		AXEG ABCDE 2
		TNT1 A 6
		AXEG A 0 A_Lower
		Wait
	Ready:
		AXEG A 1 A_WeaponReady
		loop
	Fire:
		AXEG ABCDE 3
		TNT1 A 4
		AXEG I 2 A_PlaySound("weapons/axeswing", CHAN_WEAPON)
		AXEG A 0 A_JumpIfInventory("PowerStrength", 1, "ToughFire")
		AXEG J 2 A_CustomPunch(20+random(4, 16), 1, 0, "AxeHitPuff")
		AXEG K 2
		TNT1 A 4
		AXEG EDCBA 2
		AXEG A 6
		Goto Ready
	ToughFire:
		AXEG J 2 A_CustomPunch(60+random(4, 16), 1, 0, "AxeHitPuff")
		Goto Fire+9
	AltFire:
		AXEG ABCDE 3
		TNT1 A 8
		TNT1 A 0 A_GiveInventory("AxeCharge", 9)
	AltHold:
		AXEG F 2 A_GiveInventory("AxeCharge", 1)
		TNT1 A 0 A_Refire
		AXEG G 3
		AXEG H 3 A_PlaySound("weapons/axeswing", CHAN_WEAPON)
		AXEG I 2
		AXEG A 0 A_JumpIfInventory("PowerStrength", 1, "AltFireHitBerserk")
		AXEG J 2 A_CustomPunch(56 * CallACS("Axe charge") * 0.1 + random(4, 14), 1, 0, "AxeHitPuff")
		Goto AltFireEnd
	AltFireHitBerserk:
		AXEG J 2 A_CustomPunch(112 * CallACS("Axe charge") * 0.1 + random(4, 20), 1, 0, "AxeHitPuff")
	AltFireEnd:
		AXEG K 2
		TNT1 A 8
		AXEG EDCBA 3
		Goto Ready
	}
}

ACTOR AxeHitPuff : BulletPuff
{
  +PUFFONACTORS
  //+NOEXTREMEDEATH --Uncomment and remove this message to prevent gibs
  SeeSound "weapons/axecut"
  AttackSound "weapons/axebang"
  ActiveSound "silent"
  States
  {
  Spawn:
    PUFF ABCD 4
    Stop
  }
}

ACTOR AxeCharge : Inventory
{
  Inventory.MaxAmount 30 // [Blue Shadow] A maximum of 3x the damage when fully charged
}

ACTOR SilverShortSword : Fist 20225
{
  //$Category Weapons
  Weapon.SelectionOrder 3700
  Weapon.Kickback 100
  Obituary "Silver Short Sword equipped"
  Tag "$TAG_FIST"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    SUNG A 1 A_WeaponReady
    Loop
  Deselect:
    SUNG A 1 A_Lower
    Loop
  Select:
    SUNG A 1 A_Raise
    Loop
  Fire:
    SUNG B 4 A_PlaySound("weapons/axeswing", CHAN_WEAPON)
    SUNG C 4 A_Punch
    SUNG D 5
    SUNG C 4
    SUNG B 5 A_ReFire
    Goto Ready
  }
}

actor dagfallskeleton 20224
{
  //$Category "Monsters"
  hitobituary "%o felt the blade of a Skeletal Warrior"
  health 80
  radius 24
  height 56
  mass 100
  +Floorclip
  painchance 180
  speed 10
  scale 0.5
  seesound "HellScreech"
  painsound "YellPain"
  deathsound "HellDeath"
  MONSTER
  +NOBLOOD
  states
  {
  Spawn:
    DFSK L 10 A_Look
    loop 
  See:
    DFSK AABBCCDD 2 A_Chase
    loop
  Melee:
    DFSK EF 3 A_FaceTarget 
    DFSK G 3 A_playsound("DagfallAxeR",chan_auto)  
    DFSK HI 3 A_CustomMeleeAttack(random(1,6)*4,"axehit",none)     
    DFSK J 3 A_FaceTarget    
    goto See
  Pain:
    DFSK K 2
    DFSK K 4 A_Pain
    goto See
  Death:
    DFSK K 0 A_JumpIf(Args[0] == 1, "XDeath")
    DFSK K 2 A_Scream
    DFSK K 2 A_Fall
    DFSK K 6 A_Noblocking
    DFSK O 6 A_SpawnItemEx("dagfallskeletonshield", 0, -2, 15, 0, -6, 0)
    DFSK O 1 A_SpawnItemEx("dagfallskeletonaxe", 0, 2, 8, 0, 6, 0)
	DFSK P 6
	DFSK Q 6
    DFSK N -1 
    stop
   XDeath:  // Over the top death is addictive.
    DFSK K 0 A_JumpIf(Args[0] == 2, "Death")
    DFSK O 4
    DFSK O 3 A_NoBlocking
    DFSK O 3 A_playsound("DagfallExplode",chan_auto)
    DFSK O 0 A_SpawnItemEx("dagfallskeletonshield", 0, -2, 15, 0, random (-7,-11), 0)
    DFSK O 0 A_SpawnItemEx("dagfallskeletonaxe", 0, 2, 8, 0, random (7,11), 0)
    DFSK O 0 A_SpawnItemEx("dagfallskeletonfoot", 0, -2, 6, 0, 2, 2)
    DFSK O 0 A_SpawnItemEx("dagfallskeletonfoot", 0, 2, 6, 0, -2, 2)
	DFSK O 0 A_SpawnItemEx("dagfallskeletonlegseg2", 0, -6, 4, random (-4,4), random (-7,7), random (3, 6))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonlegseg2", 0, 6, 4, random (-4,4), random (-7,7), random (3,6))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonlegseg1", 0, -6, 7, random (-4,4), random (-8,8), random (4, 8))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonlegseg1", 0, 6, 7, random (-4,4), random (-8,8), random (4,8))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonpelvis", 0, 0, 8, random (-5,5), random (-9,9), random (5,8))
    DFSK O 0 A_SpawnItemEx("dagfallskeletontorso", 0, 0, 8, random (-5,5), random (-9,9), random (5,9))
    DFSK O 0 A_SpawnItemEx("dagfallskeletonarmseg1", 0, 10, 9, random (-4,4), random (-8,8), random (6,9))
    DFSK O 0 A_SpawnItemEx("dagfallskeletonarmseg1", 0, -10, 9, random (-4,4), random (-8,8), random (6,9))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonarmseg2", 0, 14, 9, random (-3,3), random (-6,6), random (6,9))
    DFSK O 0 A_SpawnItemEx("dagfallskeletonarmseg2", 0, -14, 9, random (-3,3), random (-6,6), random (6,9))
	DFSK O 0 A_SpawnItemEx("dagfallskeletonhand", 0, 14, 8, random (-3,3), random (-6,6), random (6,8))
    DFSK O 0 A_SpawnItemEx("dagfallskeletonhand", 0, -14, 8, random (-3,3), random (-6,6), random (6,8))
    DFSK O 0 A_SpawnItemEx("dagfallskeletonskull", 0, 0, 11, random (-3,3), random (-5,5), random (8,11))
    DFSK RST 5
    DFSK U -1
    Stop
  Raise:
    DFSK NQPOK 4
    goto see
  Ice:
    DFSK M 5 A_FreezeDeath
    DFSK M 1 A_FreezeDeathChunks
    Wait
  }
  
}

Actor dagfallskeletonshield
{
  Radius 8
  Height 8
  Speed 3
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  deathsound "DagfallAxeFall"
  States
  {
  Spawn:
    DFSH ABCDEFGHHIJK 4 A_playsound("DagfallAxeFall",chan_auto) 
    Loop
  Death:
    DFSH L -1
    Loop
  }
}

Actor dagfallskeletonaxe
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.7
  deathsound "DagfallAxeFall"
  States
  {
  Spawn:
    DFAX AB 4 A_playsound("DagfallAxeFall",chan_auto) 
    Loop
  Death:
    DFAX C -1
    Loop
  }
}

// 7 billion BONES beyond this point!

Actor dagfallskeletonskullsmash
{
  Health 3
  Radius 8
  Height 8
  Speed 1
  +Shootable
  +Dropoff
  +NOTELEPORT
  +Floorclip
  +Missile
  scale 0.5
  deathsound "DagfallBoneDrop"
  +NOBLOOD
  States
  {
  Spawn:
    DSSK A 1
    Loop
  Death:
    DSST A 0 A_ChangeVelocity (random (-3,3), random (-3,3), random (6,8), CVF_RELATIVE|CVF_REPLACE)
	DSST A 0 A_NoBlocking
	DSST A 0 A_ChangeFlag("Shootable", FALSE)
    DSST A -1 A_SpawnItemEx("dagfallskeletonjawpiece", 0, 0, 1, random (-3,3), random (-5,5), random (8,11))
    Stop
  }
}

Actor dagfallskeletonjawpiece
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSST B 6 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSST B -1
    Stop
  }
}

Actor dagfallskeletonskull
{
  Radius 8
  Height 8
  Speed 1
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSSK ABCDEFGH 6 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSSK A 1 A_SpawnItemEx("dagfallskeletonskullsmash", 0, 0, 0, 0, 0, 0)
    Stop
  }
}

Actor dagfallskeletontorsosmash
{
  Health 5
  Radius 8
  Height 8
  Speed 1
  +Shootable
  +Dropoff
  +NOTELEPORT
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  +NOBLOOD
  States
  {
  Spawn:
    DSTO G 1
    Loop
  Death:
	DSTO G 0 A_NoBlocking
	DSTO G 0 A_ChangeFlag("Shootable", FALSE)
    DSTO G 0 A_SpawnItemEx("dagfallskeletonspine", 0, 0, 3, random (-3,3), random (-5,5), random (3,6))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonspine", 0, 0, 3, random (-3,3), random (-5,5), random (3,6))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonspine", 0, 0, 3, random (-3,3), random (-5,5), random (3,6))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonspine", 0, 0, 3, random (-3,3), random (-5,5), random (3,6))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 2, random (-3,3), random (-5,5), random (5,8))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 2, random (-3,3), random (-5,5), random (5,8))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 2, random (-3,3), random (-5,5), random (5,8))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 2, random (-3,3), random (-5,5), random (5,8))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 5, random (-3,3), random (-5,5), random (6,9))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 5, random (-3,3), random (-5,5), random (6,9))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 5, random (-3,3), random (-5,5), random (6,9))
	DSTO G 0 A_SpawnItemEx("dagfallskeletonrib", 0, 0, 5, random (-3,3), random (-5,5), random (6,9))
    Stop
  }
}

Actor dagfallskeletonspine
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSSP AB 3 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSSP A 1
    Loop
  }
}

Actor dagfallskeletonrib
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSSR AB 2 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSSR B -1 
    Loop
  }
}

Actor dagfallskeletontorso
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSTO ABCDEFGH 8 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSTO G 1 A_SpawnItemEx("dagfallskeletontorsosmash", 0, 0, 0, 0, 0, 0)
    Stop
  }
}

Actor dagfallskeletonpelvis
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSTP ABCDEFGH 8 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSTP A -1
    Loop
  }
}

Actor dagfallskeletonarmseg1
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSAS ABCDE 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSAS C -1
    Loop
  }
}

Actor dagfallskeletonarmseg2
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSAS FDHI 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSAS H -1
    Loop
  }
}

Actor dagfallskeletonhand
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSHD ABCD 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSHD C -1
    Loop
  }
}

Actor dagfallskeletonlegseg1
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSLS ABCD 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSLS C -1
    Loop
  }
}

Actor dagfallskeletonlegseg2
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DSLS EFGH 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DSLS G -1
    Loop
  }
}

Actor dagfallskeletonfoot
{
  Radius 8
  Height 8
  Speed 1
  +Corpse
  +Dropoff
  +NOTELEPORT
  +DoomBounce
  +DropOff
  +Floorclip
  +Missile
  scale 0.5
  States
  {
  Spawn:
    DFSF A 4 A_playsound("DagfallBoneDrop",chan_auto)
    Loop
  Death:
    DFSF A -1
    Loop
  }
}

Actor CANDA 225
{
  +FloorClip
  +Solid
  Height 10
  Radius 10
  States
  {
  Spawn:
    CAND A -1
    Stop
  }
}

Actor CHAIA 226
{
  +FloorClip
  +Solid
  Height 10
  Radius 10
  States
  {
  Spawn:
    CHAI A -1
    Stop
  }
}
ACTOR GiantRat 13200
{
  Health 30
  Speed 10
  Radius 16
  Height 24
  Mass 50
  Monster
  +FLOORCLIP
  SeeSound "rat/squeek"
  DeathSound "rat/death"
  ActiveSound "rat/active"
  Obituary "%o was gnawed on by a giant rat."
  States
  {
  Spawn:
    RATS A 10 A_Look
    Loop
  See:
    RATS BBCCDDEE 2 A_Chase
    Loop
  Melee:
    RATS F 4 A_FaceTarget
    RATS G 4 A_PlaySound ("Rat/Squeek")
    RATS H 4 A_CustomMeleeAttack (2, "Rat/Bite", "none")
    RATS A 8 A_FaceTarget
    Goto See
  Death:
    RATS I 3 A_ScreamAndUnblock
    RATS I 0 A_PlaySound ("Rat/Splat")
    RATS JKL 3
    RATS L -1
    stop
  Raise:
    RATS LKJI 3
    Goto See
  }
}

//0.5 player

actor MyPlayer : DoomPlayer
{
 scale 1

}